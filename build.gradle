plugins {
    id "java"
    id "application"
    id "net.ltgt.errorprone" version "4.1.0"
    id "jacoco"
    id "info.solidsoft.pitest" version "1.15.0"
}

group = "pt.up.fe.ldts"
version = "1.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

application {
    mainClass.set("pt.up.fe.ldts.pacman.Game")
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"
    testImplementation 'net.jqwik:jqwik:1.5.1'
    testImplementation "org.mockito:mockito-core:5.8.0"
    errorprone "com.google.errorprone:error_prone_core:2.30.0"
    implementation group: "com.googlecode.lanterna", name: "lanterna", version: "3.1.2"
}

compileJava {
    options.compilerArgs << "-Werror"
}

test {
    useJUnitPlatform {
        includeEngines ('junit-jupiter', 'jqwik')
    }
    testLogging {
        exceptionFormat = "full"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
}

pitest {
    junit5PluginVersion = "1.2.0"
    pitestVersion = "1.15.0"
    targetClasses = ["pt.up.fe.ldts.pacman.*"]
}
